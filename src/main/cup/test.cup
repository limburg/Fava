package saxion.pti.generated;

import java_cup.runtime.*;
import saxion.pti.ast.BuildTree;
import saxion.pti.ast.nodes.*;
import java.util.Collections;
import java.util.LinkedList;

action code {: 	
	// Build Tree met java code, zodat onze CUP file netjes blijft & onze code compileert en makkelijk te debuggen is.
	private BuildTree buildTree = new BuildTree();

:};
parser code {::};

/**
 * Terminal definitie
 */
terminal Symbol  ELSE, WHILE, IF, SYM_BOOLEAN, STATIC, RETURN;
terminal Symbol  NEQ, EQ, EQEQ, PLUS, MINUS, ASTERICK, MOD;
terminal Symbol  FSLASH, BSLASH, LESS, LESSEQ, GREATER, GREATEREQ;
terminal Symbol  LPAREN, RPAREN, LBRACE, RBRACE,  LSQPAREN, RSQPAREN;
terminal Symbol  COMMA, SEMICOLON, VOID, SYM_INT, SYM_STRING, FALSE, TRUE, PRINT;
terminal Integer STATIC_INT;
terminal Symbol  FUNC_PRINTLN;
terminal String IDENTIFIER;
terminal String STATIC_STRING;

/**
 * Non Terminal definitie.
 */
non terminal BuildTree					 program;
non terminal AbstractNode 				 program_;
non terminal AbstractNode				 proc_decl, func_decl, var_decl;

non terminal Integer					 var_type, var_array;
non terminal LinkedList<String> 		 var_names;

non terminal LinkedList<VariableNode> 	 param_decl;
non terminal AbstractNode				 scope_body, code_print, code_block;
non terminal LinkedList<ExpressionNode>	 code_args;
non terminal CallNode					 code_proc;
non terminal AssignmentNode				 code_assign;
non terminal IfNode				 		 if_decl,else_decl;
non terminal WhileNode 					 while_decl;

non terminal ExpressionNode				 expression, expr_test, expr_test_, expr_term;
non terminal AbstractNode				 expr_fact, expr_var, expr_func;
non terminal Integer					 expr_oper;

/**
 * Voorkeur in symbolen
 */
precedence left PLUS, MINUS;
precedence left ASTERICK, FSLASH, MOD;
precedence right ELSE;


/**
 * Grammatica regels
 */
start with program;

program		::= program_ {: RESULT = buildTree; :};
program_	::= var_decl program_  |
				proc_decl program_ |
				func_decl program_ |;

// Procedure
proc_decl 	::= 
				VOID IDENTIFIER:id 
				LPAREN param_decl:params RPAREN
				{: 
					buildTree.pushNode(new ProcedureNode(buildTree.getCurrentNode(),id, params)); 
				:} 
				LBRACE scope_body RBRACE
				{:
					buildTree.popNode();
				:}
				;


// Function
func_decl 	::= var_type:t IDENTIFIER:id 
				LPAREN param_decl:params RPAREN
				{: 
					buildTree.pushNode(buildTree.createFunctionNode(id, t, params)); 
				:} 
				LBRACE scope_body
					RETURN expression:expr SEMICOLON 
				RBRACE
				{:
					buildTree.addReturnStatement(expr);
					buildTree.popNode();
				:}
				;


// Parameters
param_decl	::= var_type:t IDENTIFIER:param var_array:a
				{:LinkedList<VariableNode> params = new LinkedList<VariableNode>();
				   params.add(buildTree.createParameter(t, param, a));
				   RESULT = params; :}|
				var_type:t IDENTIFIER:param var_array:a COMMA param_decl:prms
				{:LinkedList<VariableNode> params = new LinkedList<VariableNode>();
				   params.add(buildTree.createParameter(t, param, a));
				   params.addAll(prms);
				   RESULT = params; :}|
				{: RESULT = new LinkedList<VariableNode>(); :};
		
				
// Variabele
var_decl	::= var_type:t var_names:n var_array:a SEMICOLON				
				{: buildTree.addVariables(t,n, null, a); :}|
				var_type:t var_names:n var_array:a EQ expr_term:expr SEMICOLON
				{: buildTree.addVariables(t,n,expr, a); :};
var_names	::= IDENTIFIER:id 
				{:LinkedList<String> names = new LinkedList<String>();
				   names.add(id);
				   RESULT = names; :}|
				IDENTIFIER:id COMMA var_names:ids
				{: LinkedList<String> names = new LinkedList<String>();
				   names.add(id);
				   names.addAll(ids);
				   RESULT = names; :};
var_array	::= LSQPAREN STATIC_INT:i RSQPAREN {: RESULT = new Integer(i); :} |
				LSQPAREN RSQPAREN {: RESULT = new Integer(10); :} |;

var_type 	::= SYM_INT 		{: RESULT = sym.SYM_INT; :} | 
				SYM_STRING 		{: RESULT = sym.SYM_STRING; :} |
				SYM_BOOLEAN 	{: RESULT = sym.SYM_BOOLEAN; :};


// Code
scope_body	::= code_block | var_decl scope_body;
code_block	::= code_assign code_block |	
				code_proc code_block   |
				while_decl code_block  |
				if_decl code_block     |
				code_print code_block  |;
code_print	::= PRINT LPAREN expression:e RPAREN SEMICOLON
				{: buildTree.debugMsg("Print function called");
				   buildTree.addCode( new PrintNode(buildTree.getCurrentNode(),e)); :};
code_assign	::= IDENTIFIER:id EQ expression:e SEMICOLON 
				{: buildTree.debugMsg("Assignment to var: " + id ); 
				   buildTree.addCode( new AssignmentNode(buildTree.getCurrentNode(),id,e) ); :};
code_proc	::= IDENTIFIER:id LPAREN code_args:params RPAREN SEMICOLON
				{: buildTree.debugMsg("Call to proc/func: " + id ); 
				   buildTree.addCode( new CallNode(buildTree.getCurrentNode(),id,params) ); :};
code_args ::= expression:e 
				{: LinkedList<ExpressionNode> d =  new LinkedList<ExpressionNode>();
				   d.add(e);
				   RESULT = d;
				:} | 
				expression:e COMMA code_args:prs 
				{: LinkedList<ExpressionNode> d = new LinkedList<ExpressionNode>();
				   d.add(e);
				   d.addAll(prs);
				   RESULT = d;  :}|;


// While declaratie
while_decl	::= WHILE  
				{:  buildTree.pushNode(new WhileNode(buildTree.getCurrentNode())); :}
				LPAREN expr_test:expr RPAREN
				{: 	((WhileNode)buildTree.getCurrentNode()).setStatement(expr); :}
				LBRACE code_block RBRACE else_decl
				{: 	buildTree.popNode(); :};
				
// If/Else declaratie
if_decl		::= IF LPAREN expr_test:expr RPAREN
				{: 	buildTree.pushNode(new IfNode(buildTree.getCurrentNode(),expr)); :}
				LBRACE code_block RBRACE 
				{: 	buildTree.popNode(); :} else_decl;
else_decl	::= ELSE LPAREN expr_test:expr RPAREN 
				{: 	buildTree.pushNode(new IfNode(buildTree.getCurrentNode(),expr)); :}
				LBRACE code_block RBRACE 
				{: 	buildTree.popNode(); :} else_decl |
				ELSE 
				{: 	buildTree.pushNode(new IfNode(buildTree.getCurrentNode(),null)); :}
				LBRACE code_block RBRACE 
				{: 	buildTree.popNode(); :} | 
				;
				
// Topdomein van een expressie
expression	::= expr_test:e 		{:	buildTree.debugMsg("boolean Expression");
										RESULT = e; :} | 
				expr_term:e 		{: 	buildTree.debugMsg("result Expression");
										RESULT = e; :};


// Vergelijkings expressie
expr_test	::= expr_term:v EQEQ expr_term:r 
									{: RESULT = new ExpressionNode(buildTree.getCurrentNode(),v,sym.EQEQ,r); :} |
				expr_term:v NEQ expr_term:r  
									{: RESULT = new ExpressionNode(buildTree.getCurrentNode(),v,sym.NEQ,r);	 :} |
				expr_term:v LESS expr_term:r 
									{: RESULT = new ExpressionNode(buildTree.getCurrentNode(),v,sym.LESS,r);	 :} |
           		expr_term:v LESSEQ expr_term:r 
									{: RESULT = new ExpressionNode(buildTree.getCurrentNode(),v,sym.LESSEQ,r); :} |
           		expr_term:v GREATER expr_term:r 
									{: RESULT = new ExpressionNode(buildTree.getCurrentNode(),v,sym.GREATER,r);:} |
           		expr_term:v GREATEREQ expr_term:r
           							{: RESULT = new ExpressionNode(buildTree.getCurrentNode(),v,sym.GREATEREQ,r);:} ;
           		
// Term expressie
expr_term 	::= expr_fact:v expr_oper:o expr_term:r 
									{: RESULT = new ExpressionNode(buildTree.getCurrentNode(),v,o,r);
									 :} |
				expr_fact:f 		{: 
									   RESULT = new ExpressionNode(buildTree.getCurrentNode(),f); 
									 :} ;
expr_oper	::= ASTERICK 			{: RESULT = sym.ASTERICK; :} | 
				MOD 				{: RESULT = sym.MOD; :} | 
				FSLASH	 			{: RESULT = sym.FSLASH; :} |
				PLUS	 			{: RESULT = sym.PLUS; :} |  
				MINUS	 			{: RESULT = sym.MINUS; :} ;  
expr_fact	::= IDENTIFIER:id var_array:a		
									{: RESULT = new CallVarNode(buildTree.getCurrentNode(),id,a); :}|
				STATIC_INT:i 		{: RESULT = new StaticValueNode<Integer>(buildTree.getCurrentNode(),i); :} | 
				STATIC_STRING:s 	{: RESULT = new StaticValueNode<String>(buildTree.getCurrentNode(),s);  :} | 
				TRUE   				{: RESULT = new StaticValueNode<Boolean>(buildTree.getCurrentNode(),true);  :} | 
				FALSE  				{: RESULT = new StaticValueNode<Boolean>(buildTree.getCurrentNode(),false);  :} | 
				code_proc:c 		{: RESULT = c; :};

